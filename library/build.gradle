plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'kotlin-android'
}

android {
    namespace 'com.github.pelmenstar1.rangecalendar'
    compileSdk 33

    defaultConfig {
        minSdk 16
        targetSdk 33

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'

    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'org.jetbrains:annotations:23.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    implementation "androidx.core:core-ktx:1.10.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                pom {
                    name = "Range Calendar"
                    description = "Calendar for Android with support of range selection"
                    url = "https://github.com/pelmenstar1/rangeCalendar/"
                    inceptionYear = "2022"

                    licenses {
                        license {
                            name = "The MIT License"
                            url = "https://github.com/pelmenstar1/rangeCalendar/blob/master/LICENSE"
                        }
                    }

                    scm {
                        url = "https://github.com/pelmenstar1/rangeCalendar/"
                        connection = "scm:git:git://github.com/pelmenstar1/rangeCalendar.git"
                        developerConnection = "scm:git:ssh://git@github.com/pelmenstar1/rangeCalenadar.git"
                    }

                    developers {
                        developer {
                            id = "pelmenstar1"
                            name = "Oleg Khmaruk"
                            url = "https://github.com/pelmenstar1/"
                        }
                    }
                }

                groupId = 'com.github.pelmenstar1'
                artifactId = 'rangeCalendar'
                version = '0.9.3'

                afterEvaluate {
                    from components.release
                }
            }
        }
    }
}